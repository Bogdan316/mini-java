options {
    DEBUG_PARSER = true;
    OUTPUT_DIRECTORY = "/home/bogdan/git/MiniJava/src/main/java/org/example";
    MULTI = true;
    VISITOR = true;
    NODE_PACKAGE = "org.example";
    TRACK_TOKENS = true;
//    NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(MiniJavaParser)
    package org.example;
import java.util.*;
import javax.swing.tree.DefaultMutableTreeNode;
import java.util.ArrayList;
import javax.swing.JFrame;
import javax.swing.JTree;

public class MiniJavaParser {
    public static void constructJTree(DefaultMutableTreeNode parent, Node crtNode) {
        for(int i = 0; i < crtNode.jjtGetNumChildren(); i++) {
            Node child = crtNode.jjtGetChild(i);
            DefaultMutableTreeNode jNode = new DefaultMutableTreeNode(child.toString());
            parent.add(jNode);
            constructJTree(jNode, child);
        }
    }

    public static void main(String[] args) throws ParseException {
        MiniJavaParser parser = new MiniJavaParser(System.in);
        ASTProgram p = parser.Program();
        DefaultMutableTreeNode tree = new DefaultMutableTreeNode("Program");
        constructJTree(tree, p);
        DomainAnalysisVisitor visitor = new DomainAnalysisVisitor();
        p.jjtAccept(visitor, null);
        for(var cls : visitor.classTable) {
            System.out.println(cls);
        }
        System.out.println();
        for(var t : visitor.typeTable) {
            System.out.println(t);
        }
//        TypeCheckingVisitor v = new TypeCheckingVisitor();
//        p.jjtAccept(v, null);
        MiniJavaParserVisitor v = new MiniJavaPrinterVisitor();
        p.jjtAccept(v, null);
        javax.swing.SwingUtilities.invokeLater(new Runnable() {
                    public void run() {
                        JTree j = new JTree(tree);
                        JFrame f = new JFrame();
                        f.add(j);
                        f.setSize(200, 200);
                        f.setVisible(true);
                    }
                });
    }
}
PARSER_END(MiniJavaParser)

TOKEN : {
    <MAIN: "main">
    |
    <LENGTH: "length">
    |
    <STRING: "String">
    |
    <NEW: "new">
    |
    <INT: "int">
    |
    <BOOLEAN: "boolean">
    |
    <INT_ARR: "int" "[" "]">
    |
    <VOID: "void">
    |
    <PUBLIC: "public">
    |
    <CLASS: "class">
    |
    <EXTENDS: "extends">
    |
    <TRUE: "true">
    |
    <FALSE: "false">
    |
    <THIS: "this">
    |
    <STATIC: "static">
    |
    <RETURN: "return">
    |
    <IF: "if">
    |
    <ELSE: "else">
    |
    <WHILE: "while">
    |
    <SOUT: "System.out.println">
    |
    <INTEGER: (["0"-"9"])+>
    |
    <ID: ["_", "$", "a"-"z", "A"-"Z"](["_", "$", "a"-"z", "A"-"Z", "0"-"9"])*>
}

<*> SPECIAL_TOKEN : {
    " "
    |
    "\t"
    |
    "\n"
    |
    "\r"
    |
    <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    |
    <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

ASTProgram Program() #Program:
{}
{
    MainClass() (ClassDecl())* <EOF>
    {return jjtThis;}
}

//TODO: fix this
void MainClass() #MainClass:
{
    List fields = new ArrayList();
}
{
    <CLASS>
    <ID> { jjtThis.setName(token.image); }
    "{"
        <PUBLIC> <STATIC> <VOID> <MAIN> "(" <STRING> "[" "]" <ID> ")"
        "{"
            (LOOKAHEAD(2) VarDecl(fields))*
            (Statement())*
        "}"
    "}"
    {
    for(var f : fields) {
        jjtThis.addField((ASTVarDecl) f);
    }
}
}

void ClassDecl() #ClassDecl :
{
    List fields = new ArrayList();
    List methods = new ArrayList();
}
{
    <CLASS>
    <ID> { jjtThis.setName(token.image); }
    (
        "{"
            (VarDecl(fields))*
            (MethodDecl(methods))*
        "}"
        |
        <EXTENDS> <ID> { jjtThis.setParent(token.image); }
        "{"
            (VarDecl(fields))*
            (MethodDecl(methods))*
        "}"
    )
    {
        for(var f : fields) {
            jjtThis.addField((ASTVarDecl) f);
        }
        for(var m : methods) {
            jjtThis.addMethod((ASTMethodDecl) m);
        }
    }
}

void VarDecl(List<ASTVarDecl> decls) #VarDecl:
{}
{
    Type()
    <ID> { jjtThis.setName(token.image); }
    ";"
    {
        jjtThis.setTypeNode((ASTType) jjtThis.jjtGetChild(0));
        decls.add(jjtThis);
    }
}

void MethodDecl(List methods) #MethodDecl:
{
    List locals = new ArrayList();
    List paramsTypes = new ArrayList();
    List paramNames = new ArrayList();
    ASTType returnType = null;
}
{
     <PUBLIC>
     returnType = Type() { jjtThis.setReturnType(returnType); }
     <ID> { jjtThis.setName(token.image); }
     "(" (FormalList(paramsTypes, paramNames))? ")"
     "{"
            (LOOKAHEAD(2) VarDecl(locals))*
            (Statement())*
            <RETURN> Exp() ";"
     "}"
     {
       for(var paramType : paramsTypes) {
           jjtThis.addParamType(((ASTType) paramType).name);
       }
       for(var local : locals) {
           jjtThis.addLocal((ASTVarDecl) local);
       }
       for(var paramName: paramNames) {
           jjtThis.addParamName((String) paramName);
       }
       methods.add(jjtThis);
     }
}

void FormalList(List types, List params):
{
    ASTType type;
}
{
    type = Type() { types.add(type); }
    <ID> { params.add(token.image); }
    (FormalRest(types, params))*
}

void FormalRest(List types, List params):
{
    ASTType type;
}
{
    ","
    type = Type() { types.add(type); }
    <ID> { params.add(token.image); }
}

ASTType Type() #Type:
{}
{
   (<INT> | <INT_ARR> | <BOOLEAN> | <ID>) { jjtThis.setName(token.image); }
   { return jjtThis; }
}

void Statement():
{}
{
    "{" (Statement())* "}"
    |
    <IF> "(" Condition() ")" Statement() (LOOKAHEAD(2) <ELSE> Statement())?
    |
    <WHILE> "(" Condition() ")" Statement()
    |
    <SOUT> "(" Exp() ")" ";"
    |
    (
        LOOKAHEAD(2) <ID> (
            "=" Exp() ";"
            |
            "[" ExpArm() "]" "=" ExpArm() ";"
        )
        |
        Exp() ";"
    )
}

ASTExp Exp():
{
    ASTCondition condition;
}
{
    condition = Condition()
    {
        jjtThis.cond = condition;
        return jjtThis;
    }
}

ASTCondition Condition():
{
    ASTExpLog exp = null;
}
{
    (
        exp = ExpLog() { jjtThis.exp = exp; }
        |
        "!" exp = ExpLog() { jjtThis.notExp = exp; }
    )
    {
        return jjtThis;
    }
}

ASTExpLog ExpLog():
{
    ASTExpRel rel = null;
    ASTExpLog2 log = null;
}
{
    (
        rel = ExpRel() log = ExpLog2()
        |
        <TRUE> log = ExpLog2()
        |
        <FALSE> log = ExpLog2()
    )
    {
        jjtThis.log = log;
        jjtThis.rel = rel;
        return jjtThis;
    }
}

ASTExpLog2 ExpLog2():
{
    ASTExpRel rel = null;
}
{
    (
        OpLog()
        rel = ExpRel()
        ExpLog2()
        {
            jjtThis.rel = rel;
            return jjtThis;
        }
    )?
    {
        return null;
    }
}

void OpLog():
{}
{
    "&&" | "||"
}

ASTExpRel ExpRel():
{
    ASTCondition cond = null;
    ASTExpArm expArm = null;
    ASTExpArm expArm2 = null;
}
{
    (
        LOOKAHEAD(3) "(" cond = Condition() ")"
        |
        expArm = ExpArm() (OpRel() expArm2 = ExpArm())?
    )
    {
        jjtThis.cond = cond;
        jjtThis.expArm = expArm;
        jjtThis.expArm2 = expArm2;
        return jjtThis;
    }
}

void OpRel():
{}
{
    "==" | "!=" | "<" | ">" | "<=" | ">="
}

ASTExpArm ExpArm():
{
    ASTExpTerm term;
    ASTExpArm2 arm;
}
{
    term = ExpTerm() arm = ExpArm2()
    {
        jjtThis.term = term;
        jjtThis.arm = arm;
        return jjtThis;
    }
}

ASTExpArm2 ExpArm2():
{
    ASTExpTerm term = null;
    ASTExpArm2 arm = null;
}
{
    (
        OpAd()
        term = ExpTerm()
        arm = ExpArm2()
        {
            jjtThis.term = term;
            jjtThis.arm = arm;
            return jjtThis;
        }
    )?
    {
        return null;
    }
}

void OpAd():
{}
{
    "+" | "-"
}

ASTExpTerm ExpTerm():
{
    ASTExpFact fact;
    ASTExpTerm2 term;
}
{
    fact = ExpFact()
    term = ExpTerm2()
    {
        jjtThis.fact = fact;
        jjtThis.term = term;
        return jjtThis;
    }
}

ASTExpTerm2 ExpTerm2():
{
    ASTExpFact fact = null;
    ASTExpTerm2 term = null;
}
{
    (
        OpMul()
        fact = ExpFact()
        term = ExpTerm2()
        {
            jjtThis.fact = fact;
            jjtThis.term = term;
            return jjtThis;
        }
    )?
    {
        return null;
    }
}

void OpMul():
{}
{
    "*" | "|"
}

ASTExpFact ExpFact():
{
    Token integer = null;
    ASTExpArm expArm = null;
    ASTAccess access = null;
}
{
    (
        integer = <INTEGER>
        |
        "("
            expArm = ExpArm()
        ")"
        |
        access = Access()
    )
    {
        jjtThis.integer = integer;
        jjtThis.expArm = expArm;
        jjtThis.access = access;
        return jjtThis;
    }
}

ASTAccess Access():
{
    ASTCall call;
    ASTExpArm exp = null;
}
{
    call = Call() ("[" exp = ExpArm() "]")?
    {
        jjtThis.call = call;
        jjtThis.exp = exp;
        return jjtThis;
    }
}

ASTCall Call():
{
    ASTCallTarget target;
    ASTCall2 call2;
}
{
    target = CallTarget()
    call2 = Call2()
    {
        jjtThis.target = target;
        jjtThis.call2 = call2;
        return jjtThis;
    }
}

ASTCall2 Call2():
{
    Token length = null;
    Token id = null;
    List argTypes = new ArrayList();
    ASTCall2 call = null;
}
{
    (
        "."
        (
            length = <LENGTH>
            |
            id = <ID>
            "(" (ExpList(argTypes))? ")" call = Call2()
        )
        {
            jjtThis.length = length;
            jjtThis.id = id;
            jjtThis.argTypes = argTypes;
            jjtThis.call = call;
            return jjtThis;
        }
    )?
    {
        return null;
    }
}

ASTCallTarget CallTarget():
{
    Token intArr = null;
    ASTExpArm idxExp = null;
    Token clsName = null;
    Token localVar = null;
    Token thiz = null;
}
{
    (
        "new"
        (
            intArr = <INT> "[" idxExp = ExpArm() "]"
            |
            clsName = <ID> "(" ")"

        )
        |
        localVar = <ID>
        |
        thiz = <THIS>
    )
    {
        jjtThis.intArr = intArr;
        jjtThis.idxExp = idxExp;
        jjtThis.clsName = clsName;
        jjtThis.localVar = localVar;
        jjtThis.thiz = thiz;
        return jjtThis;
    }
}

void ExpList(List types):
{
    ASTExp exp;
}
{
    exp = Exp() { types.add(exp.type); } (ExpRest(types))*
}

void ExpRest(List types):
{
    ASTExp exp;
}
{
    "," exp = Exp() { types.add(exp.type); }
}

