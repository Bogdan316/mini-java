options {
//    DEBUG_PARSER = true;
    OUTPUT_DIRECTORY = "/home/bogdan/git/MiniJava/src/main/java/org/example";
    MULTI = true;
    VISITOR = true;
    NODE_PACKAGE = "org.example";
    TRACK_TOKENS = true;
//    NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(MiniJavaParser)
    package org.example;
import java.util.*;
import javax.swing.tree.DefaultMutableTreeNode;
import java.util.ArrayList;
import javax.swing.JFrame;
import javax.swing.JTree;

public class MiniJavaParser {
    public static void constructJTree(DefaultMutableTreeNode parent, Node crtNode) {
        for(int i = 0; i < crtNode.jjtGetNumChildren(); i++) {
            Node child = crtNode.jjtGetChild(i);
            DefaultMutableTreeNode jNode = new DefaultMutableTreeNode(child.toString());
            parent.add(jNode);
            constructJTree(jNode, child);
        }
    }
    public static void main(String[] args) throws ParseException {
        MiniJavaParser parser = new MiniJavaParser(System.in);
        ASTProgram p = parser.Program();
        DefaultMutableTreeNode tree = new DefaultMutableTreeNode("Program");
        constructJTree(tree, p);
        DomainAnalysisVisitor visitor = new DomainAnalysisVisitor();
        p.jjtAccept(visitor, null);
        for(var cls : visitor.classTable) {
            System.out.println(cls);
        }
        System.out.println();
        for(var t : visitor.typeTable) {
            System.out.println(t);
        }
        MiniJavaParserVisitor v = new MiniJavaPrinterVisitor();
        p.jjtAccept(v, null);
        javax.swing.SwingUtilities.invokeLater(new Runnable() {
                    public void run() {
                        JTree j = new JTree(tree);
                        JFrame f = new JFrame();
                        f.add(j);
                        f.setSize(200, 200);
                        f.setVisible(true);
                    }
                });
    }
}
PARSER_END(MiniJavaParser)

TOKEN : {
    <MAIN: "main">
    |
    <LENGTH: "length">
    |
    <STRING: "String">
    |
    <NEW: "new">
    |
    <INT: "int">
    |
    <BOOLEAN: "boolean">
    |
    <INT_ARR: "int" "[" "]">
    |
    <VOID: "void">
    |
    <PUBLIC: "public">
    |
    <CLASS: "class">
    |
    <EXTENDS: "extends">
    |
    <TRUE: "true">
    |
    <FALSE: "false">
    |
    <THIS: "this">
    |
    <STATIC: "static">
    |
    <RETURN: "return">
    |
    <IF: "if">
    |
    <ELSE: "else">
    |
    <WHILE: "while">
    |
    <SOUT: "System.out.println">
    |
    <INTEGER: (["0"-"9"])+>
    |
    <ID: ["_", "$", "a"-"z", "A"-"Z"](["_", "$", "a"-"z", "A"-"Z", "0"-"9"])*>
}

<*> SPECIAL_TOKEN : {
    " "
    |
    "\t"
    |
    "\n"
    |
    "\r"
    |
    <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    |
    <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

ASTProgram Program() #Program:
{}
{
    MainClass() (ClassDecl())* <EOF>
    {return jjtThis;}
}

//TODO: fix this
void MainClass() #MainClass:
{
    List fields = new ArrayList();
}
{
    <CLASS>
    <ID> { jjtThis.setName(token.image); }
    "{"
        <PUBLIC> <STATIC> <VOID> <MAIN> "(" <STRING> "[" "]" <ID> ")"
        "{"
            (LOOKAHEAD(2) VarDecl(fields))*
            (Statement())*
        "}"
    "}"
    {
    for(var f : fields) {
        jjtThis.addField((ASTVarDecl) f);
    }
}
}

void ClassDecl() #ClassDecl :
{
    List fields = new ArrayList();
    List methods = new ArrayList();
}
{
    <CLASS>
    <ID> { jjtThis.setName(token.image); }
    (
        "{"
            (VarDecl(fields))*
            (MethodDecl(methods))*
        "}"
        |
        <EXTENDS> <ID> { jjtThis.setParent(token.image); }
        "{"
            (VarDecl(fields))*
            (MethodDecl(methods))*
        "}"
    )
    {
        for(var f : fields) {
            jjtThis.addField((ASTVarDecl) f);
        }
        for(var m : methods) {
            jjtThis.addMethod((ASTMethodDecl) m);
        }
    }
}

void VarDecl(List<ASTVarDecl> decls) #VarDecl:
{}
{
    Type()
    <ID> { jjtThis.setName(token.image); }
    ";"
    {
        jjtThis.setTypeNode((ASTType) jjtThis.jjtGetChild(0));
        decls.add(jjtThis);
    }
}

void MethodDecl(List methods) #MethodDecl:
{
    List locals = new ArrayList();
    List paramsTypes = new ArrayList();
    List paramNames = new ArrayList();
    ASTType returnType = null;
}
{
     <PUBLIC>
     returnType = Type() { jjtThis.setReturnType(returnType); }
     <ID> { jjtThis.setName(token.image); }
     "(" (FormalList(paramsTypes, paramNames))? ")"
     "{"
            (LOOKAHEAD(2) VarDecl(locals))*
            (Statement())*
            <RETURN> Exp() ";"
     "}"
     {
   for(var paramType : paramsTypes) {
       jjtThis.addParamType(((ASTType) paramType).name);
   }
   for(var local : locals) {
       jjtThis.addLocal((ASTVarDecl) local);
   }
   for(var paramName: paramNames) {
       jjtThis.addParamName((String) paramName);
   }
   methods.add(jjtThis);
}
}

void FormalList(List types, List params):
{
    ASTType type;
}
{
    type = Type() { types.add(type); }
    <ID> { params.add(token.image); }
    (FormalRest(types, params))*
}

void FormalRest(List types, List params):
{
    ASTType type;
}
{
    ","
    type = Type() { types.add(type); }
    <ID> { params.add(token.image); }
}

ASTType Type() #Type:
{}
{
   (<INT> | <INT_ARR> | <BOOLEAN> | <ID>) { jjtThis.setName(token.image); }
   { return jjtThis; }
}

void Statement():
{}
{
    "{" (Statement())* "}"
    |
    <IF> "(" Condition() ")" Statement() (LOOKAHEAD(2) <ELSE> Statement())?
    |
    <WHILE> "(" Condition() ")" Statement()
    |
    <SOUT> "(" Exp() ")" ";"
    |
    (
        LOOKAHEAD(2) <ID> (
            "=" Exp() ";"
            |
            "[" ExpArm() "]" "=" ExpArm() ";"
        )
        |
        Exp() ";"
    )
}

String Exp():
{}
{
    Condition()
    {
        return "Exp";
    }
}

void Condition():
{}
{
    ExpLog() | "!" ExpLog()
}

void ExpLog():
{}
{
    ExpRel() ExpLog2() | <TRUE> ExpLog2() | <FALSE> ExpLog2()
}

void ExpLog2():
{}
{
    (OpLog() ExpRel() ExpLog2())?
}

void OpLog():
{}
{
    "&&" | "||"
}

void ExpRel():
{}
{
    LOOKAHEAD(3) "(" Condition() ")" | ExpArm() (OpRel() ExpArm())?
}

void OpRel():
{}
{
    "==" | "!=" | "<" | ">" | "<=" | ">="
}

void ExpArm():
{}
{
    ExpTerm() ExpArm2()
}

void ExpArm2():
{}
{
    (OpAd() ExpTerm() ExpArm2())?
}

void OpAd():
{}
{
    "+" | "-"
}

void ExpTerm():
{}
{
    ExpFact() ExpTerm2()
}

void ExpTerm2():
{}
{
    (OpMul() ExpFact() ExpTerm2())?
}

void OpMul():
{}
{
    "*" | "|"
}

void ExpFact():
{}
{
    <INTEGER> | "(" ExpArm() ")" | Access()
}

void Access():
{}
{
    Call() ("[" ExpArm() "]")?
}

void Call():
{}
{
    CallTarget() Call2()
}

void Call2():
{}
{
    (
        "."
        (
            <LENGTH>
            |
            <ID>
            "(" (ExpList())? ")" Call2()
        )
    )?
}

void CallTarget():
{}
{
    // TODO: distinction between local var and type
    "new"
    (
        <INT> "[" ExpArm() "]"
        |
        <ID> "(" ")"
    )
    |
    <ID>
    |
    <THIS>
}

void ExpList():
{}
{
    Exp() (ExpRest())*
}

void ExpRest():
{}
{
    "," Exp()
}

