options {
//    DEBUG_PARSER = true;
    OUTPUT_DIRECTORY = "/home/bogdan/git/MiniJava/src/main/java/org/example";
    MULTI = true;
    VISITOR = true;
    NODE_PACKAGE = "org.example";
    TRACK_TOKENS = true;
    NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(MiniJavaParser)
    package org.example;
    import java.util.*;
    import javax.swing.tree.DefaultMutableTreeNode;import java.util.ArrayList;
    public class MiniJavaParser {
        public static void constructJTree(DefaultMutableTreeNode parent, Node crtNode) {
            for(int i = 0; i < crtNode.jjtGetNumChildren(); i++) {
                Node child = crtNode.jjtGetChild(i);
                DefaultMutableTreeNode jNode = new DefaultMutableTreeNode(child.toString());
                parent.add(jNode);
                constructJTree(jNode, child);
            }
        }
        public static void main(String[] args) throws ParseException {
            MiniJavaParser parser = new MiniJavaParser(System.in);
            ASTProgram p = parser.Program();
            DefaultMutableTreeNode tree = new DefaultMutableTreeNode("Program");
            constructJTree(tree, p);
            DomainAnalysisVisitor visitor = new DomainAnalysisVisitor();
            p.jjtAccept(visitor, null);
            for(var cls : visitor.classTable) {
                System.out.println(cls);
            }
            System.out.println();
            for(var t : visitor.typeTable) {
                System.out.println(t);
            }
//            MiniJavaParserVisitor v = new MiniJavaPrinterVisitor();
//            p.jjtAccept(v, null);
//            javax.swing.SwingUtilities.invokeLater(new Runnable() {
//                        public void run() {
//                            JTree j = new JTree(tree);
//                            JFrame f = new JFrame();
//                            f.add(j);
//                            f.setSize(200, 200);
//                            f.setVisible(true);
//                            System.out.println("here");
//                        }
//                    });
        }
    }
PARSER_END(MiniJavaParser)

TOKEN : {
    <MAIN: "main">
    |
    <LENGTH: "length">
    |
    <STRING: "String">
    |
    <NEW: "new">
    |
    <INT: "int">
    |
    <BOOLEAN: "boolean">
    |
    <INT_ARR: "int" "[" "]">
    |
    <VOID: "void">
    |
    <PUBLIC: "public">
    |
    <CLASS: "class">
    |
    <EXTENDS: "extends">
    |
    <TRUE: "true">
    |
    <FALSE: "false">
    |
    <THIS: "this">
    |
    <STATIC: "static">
    |
    <RETURN: "return">
    |
    <IF: "if">
    |
    <ELSE: "else">
    |
    <WHILE: "while">
    |
    <SOUT: "System.out.println">
    |
    <INTEGER: (["0"-"9"])+>
    |
    <ID: ["_", "$", "a"-"z", "A"-"Z"](["_", "$", "a"-"z", "A"-"Z", "0"-"9"])*>
}

<*> SKIP : {
    " "
    |
    "\t"
    |
    "\n"
    |
    "\r"
    |
    <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    |
    <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

ASTProgram Program() #Program:
{}
{
    MainClass() (ClassDecl())* <EOF>
    {return jjtThis;}
}

void MainClass() #MainClass:
{
    List fields = new ArrayList();
}
{
    <CLASS>
    <ID> { jjtThis.setName(token.image); }
    "{"
        <PUBLIC> <STATIC> <VOID> <MAIN> "(" <STRING> "[" "]" <ID> ")"
        "{"
            (LOOKAHEAD(2) VarDecl(fields))*
            (Statement())*
        "}"
    "}"
    {
    for(var f : fields) {
        jjtThis.addField((ASTVarDecl) f);
    }
}
}

void ClassDecl() #ClassDecl :
{
    List fields = new ArrayList();
    List methods = new ArrayList();
}
{
    <CLASS>
    <ID> { jjtThis.setName(token.image); }
    (
        "{"
            (VarDecl(fields))*
            (MethodDecl(methods))*
        "}"
        |
        <EXTENDS> <ID> { jjtThis.setParent(token.image); }
        "{"
            (VarDecl(fields))*
            (MethodDecl(methods))*
        "}"
    )
    {
        for(var f : fields) {
            jjtThis.addField((ASTVarDecl) f);
        }
        for(var m : methods) {
            jjtThis.addMethod((ASTMethodDecl) m);
        }
    }
}

void VarDecl(List<ASTVarDecl> decls) #VarDecl:
{}
{
    Type()
    <ID> { jjtThis.setName(token.image); }
    ";"
    {
        jjtThis.setTypeNode((ASTType) jjtThis.jjtGetChild(0));
        decls.add(jjtThis);
    }
}

void MethodDecl(List methods) #MethodDecl:
{
    List locals = new ArrayList();
    List paramsTypes = new ArrayList();
    List paramNames = new ArrayList();
    ASTType returnType = null;
}
{
     <PUBLIC>
     returnType = Type() { jjtThis.setReturnType(returnType); }
     <ID> { jjtThis.setName(token.image); }
     "(" (FormalList(paramsTypes, paramNames))? ")"
     "{"
            (LOOKAHEAD(2) VarDecl(locals))*
            (Statement())*
            <RETURN> Exp() ";"
     "}"
     {
   for(var paramType : paramsTypes) {
       jjtThis.addParamType(((ASTType) paramType).name);
   }
   for(var local : locals) {
       jjtThis.addLocal((ASTVarDecl) local);
   }
   for(var paramName: paramNames) {
       jjtThis.addParamName((String) paramName);
   }
   methods.add(jjtThis);
}
}

void FormalList(List types, List params):
{
    ASTType type;
}
{
    type = Type() { types.add(type); }
    <ID> { params.add(token.image); }
    (FormalRest(types, params))*
}

void FormalRest(List types, List params):
{
    ASTType type;
}
{
    ","
    type = Type() { types.add(type); }
    <ID> { params.add(token.image); }
}

ASTType Type() #Type:
{}
{
   (<INT> | <INT_ARR> | <BOOLEAN> | <ID>) { jjtThis.setName(token.image); }
   { return jjtThis; }
}

void Statement():
{}
{
    "{" (Statement())* "}"
    |
    <IF> "(" Exp() ")" Statement() (LOOKAHEAD(2) <ELSE> Statement())?
    |
    <WHILE> "(" Exp() ")" Statement()
    |
    <SOUT> "(" Exp() ")" ";"
    |
    (
        LOOKAHEAD(2) <ID> (
            "=" Exp() ";"
            |
            "[" Exp() "]" "=" Exp() ";"
        )
        |
        Exp() ";"
    )
}

void Exp():
{}
{
    Condition()
}

void Condition():
{}
{
    LogExp()
}

void ExpList():
{}
{
    LogExp() (ExpRest())*
}

void ExpRest():
{}
{
    "," LogExp()
}

void LogExp():
{}
{
    LogExpTerm() (LOOKAHEAD(2) "||" LogExpTerm())*
}

void LogExpTerm():
{}
{
    LogExpFactor() (LOOKAHEAD(2) "&&" LogExpFactor())*
}

void LogExpFactor():
{}
{
    "!" LogExp() | LOOKAHEAD(3) "(" LogExp() ")" | RelExp() | <TRUE> | <FALSE>
}


void RelExp():
{}
{
    ArExp() (RelOp() ArExp())?
}

void RelOp():
{}
{
    "<" | ">" | "<=" | ">=" | "==" | "!="
}

void ArExp():
{}
{
    Term() (AddOp() Term())*
}

void AddOp():
{}
{
    "+" | "-"
}

void Term():
{}
{
    Factor() (MulOp() Factor())*
}

void MulOp():
{}
{
    "*" | "/" | "."
}

void Factor():
{}
{
    "(" ArExp() ")"
    | <ID> (LOOKAHEAD(2) "(" (ExpList())? ")")?
    | <INTEGER>
    | <NEW> (<ID> "(" ")" | <INT> "[" ArExp() "]")
    | <THIS>
}
